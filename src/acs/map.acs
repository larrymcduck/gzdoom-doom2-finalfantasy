#library "map"
#include "zcommon.acs"
#import "vars.acs"
#import "shops.acs"
#import "map01_world.acs"
#import "map02_cornelia.acs"

int canMoveUp = 1;
int canMoveDown = 1;
int canMoveLeft = 1;
int canMoveRight = 1;
int canMoveUpRight = 1;
int canMoveDownRight = 1;
int canMoveUpLeft = 1;
int canMoveDownLeft = 1;

int mapDisplayX = 0;
int mapDisplayY = 0;

int mapSize = 0;

int sameMapX = -1;
int sameMapY = -1;

int moveX = 0;
int moveY = 0;

int moveSpeed = 4;

bool showingMessage = false;

function void saveWorldMapCoordinates(void)
{
    worldMapX = heroX;
    worldMapY = heroY;
}

function void show_map(void)
{
    mapSize = get_map_size();
    
    str map = StrParam(s:"map", s:pad0s(nextMap), d:currentMapNum);

    // if map level > 1, use StrParam to add s:"_", d:2

    heroX = GetActorX(0) >> 16;
    heroY = GetActorY(0) >> 16;

    mapDisplayX = ((mapSize / 2) + hudHalfWidth - (((GetActorX(0) / 32) * 16) >> 16)) << 16;
    mapDisplayY = ((mapSize / 2) + hudHalfHeight + (((GetActorY(0) / 32) * 16) >> 16)) << 16;

    SetHudSize(320, 240, FALSE);

    show_graphic(map, MAP_LAYER, mapDisplayX, mapDisplayY);
    show_graphic(map, MAP_LAYER + 1, mapDisplayX - (mapSize << 16), mapDisplayY - (mapSize << 16));
    show_graphic(map, MAP_LAYER + 2, mapDisplayX, mapDisplayY - (mapSize << 16));
    show_graphic(map, MAP_LAYER + 3, mapDisplayX + (mapSize << 16), mapDisplayY - (mapSize << 16));
    show_graphic(map, MAP_LAYER + 4, mapDisplayX - (mapSize << 16), mapDisplayY);
    show_graphic(map, MAP_LAYER + 5, mapDisplayX + (mapSize << 16), mapDisplayY);
    show_graphic(map, MAP_LAYER + 6, mapDisplayX - (mapSize << 16), mapDisplayY + (mapSize << 16));
    show_graphic(map, MAP_LAYER + 7, mapDisplayX, mapDisplayY + (mapSize << 16));
    show_graphic(map, MAP_LAYER + 8, mapDisplayX + (mapSize << 16), mapDisplayY + (mapSize << 16));

    map_objects();
    hero_map_movement();

    if (showHero)
    {
      str heroMapSprite = StrParam(s:heroMap, s:"_map", s:heroDirection, d:heroMapState);
      show_graphic(heroMapSprite, HERO_MAP_LAYER, (hudHalfWidth << 16), ((hudHalfHeight - 3) << 16));
    }
    else
    {
      clear_graphic_layer(HERO_MAP_LAYER);
    }
}

function void check_movement(void) {
    SetActorAngle(0, 0.0);
    canMoveRight = Spawn("WallCheck", GetActorX(0) + 32.0, GetActorY(0), 0.0);
    canMoveLeft = Spawn("WallCheck", GetActorX(0) - 32.0, GetActorY(0), 0.0);
    canMoveDown = Spawn("WallCheck", GetActorX(0), GetActorY(0) - 32.0, 0.0);
    canMoveUp = Spawn("WallCheck", GetActorX(0), GetActorY(0) + 32.0, 0.0);

    canMoveUpRight = Spawn("WallCheck", GetActorX(0) + 32.0, GetActorY(0) + 32.0, 0.0);
    canMoveDownRight = Spawn("WallCheck", GetActorX(0) + 32.0, GetActorY(0) - 32.0, 0.0);
    canMoveUpLeft = Spawn("WallCheck", GetActorX(0) - 32.0, GetActorY(0) + 32.0, 0.0);
    canMoveDownLeft = Spawn("WallCheck", GetActorX(0) - 32.0, GetActorY(0) - 32.0, 0.0);
}

function void check_map_loop(void)
{
    int newX = GetActorX(0);
    int newY = GetActorY(0);
    int newZ = GetActorZ(0);
    if (newX == (-16 << 16))
    {
        newX = ((mapSize * 2) - 16) << 16;
    }

    if (newX == ((mapSize * 2) + 16) << 16)
    {
        newX = (16 << 16);
    }

    if (newY == (16 << 16))
    {
        newY = -(((mapSize * 2) - 16) << 16);
    }

    if (newY == -(((mapSize * 2) + 16) << 16))
    {
        newY = (16 << 16);
    }

    SetActorPosition(0, newX, newY, newZ, 0);
}

function void hero_map_movement(void)
{
    Thing_Stop(0);
    if (!screenChanging && CheckInventory("MovedOnce"))
    {
        check_map_points();
    }

    if (canHeroMove && !showingMessage)
    {
        //int buttons = GetPlayerInput(0, INPUT_BUTTONS);
        canHeroMove = false;
        check_map_loop();
        check_movement();

        if ((buttons & BT_FORWARD) && !(buttons & BT_BACK) && canMoveUp)  // move up
        {
            heroDirection = "u";
            moveY++;
        }
        if ((buttons & BT_BACK) && !(buttons & BT_FORWARD) && canMoveDown)  // move down
        {
            heroDirection = "d";
            moveY--;
        }

        if ((buttons & BT_MOVELEFT) && !(buttons & BT_MOVERIGHT) && canMoveLeft)  // move left
        {
            heroDirection = "l";
            moveX--;
        }
        if ((buttons & BT_MOVERIGHT) && !(buttons & BT_MOVELEFT) && canMoveRight)  // move right
        {
            heroDirection = "r";
            moveX++;
        }

        if (moveY == 1 && ((buttons & BT_MOVELEFT) || (buttons & BT_MOVERIGHT)))  // up
        {
            if ((moveX == -1 && !canMoveUpLeft) || (moveX == 1 && !canMoveUpRight))
            {
                heroDirection = "u";
                moveX = 0;
            }
        }
        else if (moveY == -1 && ((buttons & BT_MOVELEFT) || (buttons & BT_MOVERIGHT)))  // down
        {
            if ((moveX == -1 && !canMoveDownLeft) || (moveX == 1 && !canMoveDownRight))
            {
                heroDirection = "d";
                moveX = 0;
            }
        }

        if (moveX == -1 && ((buttons & BT_FORWARD) || (buttons & BT_BACK)))  // left
        {
            if ((moveY == -1 && !canMoveDownLeft) || (moveY == 1 && !canMoveUpLeft))
            {
                heroDirection = "l";
                moveY = 0;
            }
        }
        else if (moveX == 1 && ((buttons & BT_FORWARD) || (buttons & BT_BACK)))  // right
        {
            if ((moveY == -1 && !canMoveDownRight) || (moveY == 1 && !canMoveUpRight))
            {
                heroDirection = "r";
                moveY = 0;
            }
        }
        
        nextMapX = GetActorX(0) + ((moveX * 32) << 16);
        nextMapY = GetActorY(0) + ((moveY * 32) << 16);

        // (buttons & BT_USE) || (buttons & BT_ALTATTACK) || (buttons & BT_JUMP)

        if (buttons & BT_JUMP)  // running
        {
          moveSpeed = 8;
        }
        else
        {
          moveSpeed = 4;
        }

    }

    if (GetActorX(0) == nextMapX && GetActorY(0) == nextMapY)
    {
        canHeroMove = !screenChanging;
        moveX = 0;
        moveY = 0;
        heroMapState = 1;
        heroMapStateTics = 0;

        if (!screenChanging && !showingMessage)
        {
            if ((buttons & BT_FORWARD) && !(buttons & BT_BACK))  // look up
            {
                heroDirection = "u";
            }
            else if ((buttons & BT_BACK) && !(buttons & BT_FORWARD))  // look down
            {
                heroDirection = "d";
            }
            else if ((buttons & BT_MOVELEFT) && !(buttons & BT_MOVERIGHT))  // look left
            {
                heroDirection = "l";
            }
            else if ((buttons & BT_MOVERIGHT) && !(buttons & BT_MOVELEFT))  // look right
            {
                heroDirection = "r";
            }
        }
    }
    else if (!screenChanging)
    {
        GiveInventory("MovedOnce", 1);
        SetActorVelocity(0, (moveX << 16) * moveSpeed, (moveY << 16) * moveSpeed, 0, 0, 0);

        heroMapStateTics++;
        if (heroMapStateTics > ((32 / moveSpeed) / 2))
        {
            heroMapState = 1;
        }
        else
        {
            heroMapState = 2;
        }
    }
}

function int get_map_size(void)
{
    switch(currentMapNum)
    {
        case 1:
            return 4096;
        case 2:
            return 1024;
    }

    return -1;
}

function int mapDisplayCoordinatesX(int x)
{
    return mapDisplayX - ((mapSize / 2) << 16) + (x << 16);
}

function int mapDisplayCoordinatesY(int y)
{
    return mapDisplayY - ((mapSize / 2) << 16) + (y << 16);
}

function void map_objects(void)
{
    switch(currentMapNum)
    {
        case 1:
            map01_objects();
            return;
        case 2:
            map02_objects();
            return;
    }
}

function void check_map_points(void)
{
    nextMap = currentMapNum;
    nextMapStart = 0;

    switch(currentMapNum)
    {
        case 1:
            map01_points();
            break;
        case 2:
            map02_points();
            break;
    }

    screenChanging = (nextMap != currentMapNum) || inShop;
}

function void set_next_map(int nextMapNum, str nextMapHeroDir)
{
    changingMap = true;
    TakeInventory("MovedOnce", 1);
    if (currentMapNum == 1)
    {
        saveWorldMapCoordinates();
    }

    nextMap = nextMapNum;
    nextHeroDirection = nextMapHeroDir;
}

script "try_interact" (void)
{
  if (!canHeroMove)
  {
    terminate;  
  }
  else if (showingMessage)
  {
    clear_message();
    terminate;
  }
  
  int spawnX = heroX;
  int spawnY = heroY;

  if (heroDirection == "u")
  {
    spawnY += 32;
  }
  else if (heroDirection == "d")
  {
    spawnY -= 32;
  }
  else if (heroDirection == "l")
  {
    spawnX -= 32;
  }
  else if (heroDirection == "r")
  {
    spawnX += 32;
  }
  
  SpawnForced("InteractSpawn", spawnX << 16, spawnY << 16, GetActorZ(0) + 32.0);
}

script "interact" (int entity_map, int entity)
{
  // face hero

  SetActivator(0, AAPTR_PLAYER1);

  switch(entity_map)
  {
    case 2:
      map02_interact(entity);
      break;
  }
}

function void show_message_lump(str lump)
{
  showingMessage = true;
  show_graphic("msg_back", 101, 160.0, 50.0);

  SetHudSize(320, 240, FALSE);
  SetFont("ffnew");
  HudMessage(l:lump; HUDMSG_PLAIN, 100, "CR_UNTRANSLATED", 160.0, 50.0, 0);
}

function void clear_message(void)
{
  showingMessage = false;
  clear_graphic_layer(101);
  clear_graphic_layer(100);
}

function void show_door(str door_graphic, int layer, int x, int y)
{
  if (heroX == x * 2 && ((-heroY == y * 2) || (-(nextMapY >> 16) == y * 2)))
  {
    clear_graphic_layer(layer);
  }
  else
  {
    show_graphic(door_graphic, layer, mapDisplayCoordinatesX(x), mapDisplayCoordinatesY(y));
  }
}
